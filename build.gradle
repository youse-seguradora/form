apply plugin: 'com.github.ben-manes.versions'

buildscript {
    ext.versions = [
            'minSdk'          : 16,
            'compileSdk'      : 28,
            'targetSdk'       : 28,

            'kotlin'          : '1.3.21',

            'androidPlugin'   : '3.3.2',

            'supportLibrary'  : '28.0.0',
            'androidArch'     : '1.1.1',
            'rxJava'          : '2.2.2',
            'rxBinding'       : '2.1.1',
            'constraintLayout': '1.1.2',
            'junit'           : '4.12'
    ]

    ext.deps = [
            androidPlugin: "com.android.tools.build:gradle:${versions.androidPlugin}",
            kotlin       : [
                    stdlib: [
                            'common': "org.jetbrains.kotlin:kotlin-stdlib-common:${versions.kotlin}",
                            'jdk'   : "org.jetbrains.kotlin:kotlin-stdlib:${versions.kotlin}",
                            'js'    : "org.jetbrains.kotlin:kotlin-stdlib-js:${versions.kotlin}",
                    ],
                    test  : [
                            'common'     : "org.jetbrains.kotlin:kotlin-test-common:${versions.kotlin}",
                            'annotations': "org.jetbrains.kotlin:kotlin-test-annotations-common:${versions.kotlin}",
                            'jdk'        : "org.jetbrains.kotlin:kotlin-test-junit:${versions.kotlin}",
                            'js'         : "org.jetbrains.kotlin:kotlin-test-js:${versions.kotlin}",
                    ],
            ],
            rx           : [
                    java   : "io.reactivex.rxjava2:rxjava:${versions.rxJava}",
                    binding: "com.jakewharton.rxbinding2:rxbinding-kotlin:${versions.rxBinding}",
            ],
            android      : [
                    support: [
                            appcompat       : "com.android.support:appcompat-v7:${versions.supportLibrary}",
                            design          : "com.android.support:design:${versions.supportLibrary}",
                            constraintLayout: "com.android.support.constraint:constraint-layout:${versions.constraintLayout}"
                    ],
                    arch   : [
                            lifecycle: [
                                    livedata       : "android.arch.lifecycle:livedata:${versions.androidArch}",
                                    extensions     : "android.arch.lifecycle:extensions:${versions.androidArch}",
                                    reactivestreams: "android.arch.lifecycle:reactivestreams:${versions.androidArch}"
                            ],
                            core     : [testing: "android.arch.core:core-testing:${versions.androidArch}"]
                    ]
            ],
            annotations  : 'org.jetbrains:annotations:16.0.3',

            junit        : "junit:junit:${versions.junit}"
    ]

    repositories {
        google()
        jcenter()
        gradlePluginPortal()
        maven { url 'https://dl.bintray.com/kotlin/kotlin-eap' }
    }

    dependencies {
        classpath deps.androidPlugin
        classpath 'com.github.ben-manes:gradle-versions-plugin:0.20.0'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:${versions.kotlin}"
        classpath "org.jetbrains.kotlin:kotlin-frontend-plugin:0.0.45"
    }
}

subprojects {
    repositories {
        google()
        mavenCentral()
        jcenter()
    }

    tasks.withType(Test) {
        testLogging {
            events "passed", "skipped", "failed", "standard_error", "standard_out"
            exceptionFormat "full"
            showExceptions true
            showStackTraces true
            showCauses true
        }
    }
}
