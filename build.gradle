apply plugin: 'com.github.ben-manes.versions'

buildscript {
    ext.versions = [
            'minSdk'          : 16,
            'compileSdk'      : 28,
            'targetSdk'       : 28,

            'kotlin'          : '1.2.60',

            'androidPlugin'   : '3.1.3',
            'androidTools'    : '26.1.3',

            'supportLibrary'  : '27.1.1',
            'androidArch'     : '1.1.1',
            'rxJava'          : '2.1.7',
            'rxBinding'       : '2.1.1',
            'constraintLayout': '1.1.2',
            'junit'           : '4.12'
    ]

    ext.deps = [
            androidPlugin           : "com.android.tools.build:gradle:${versions.androidPlugin}",
            databindingCompiler: "com.android.databinding:compiler:${versions.androidPlugin}",
            kotlin                  : [
                    stdlib: [
                            'common': "org.jetbrains.kotlin:kotlin-stdlib-common:${versions.kotlin}",
                            'jdk'   : "org.jetbrains.kotlin:kotlin-stdlib:${versions.kotlin}",
                            'js'    : "org.jetbrains.kotlin:kotlin-stdlib-js:${versions.kotlin}",
                    ],
                    test  : [
                            'common'     : "org.jetbrains.kotlin:kotlin-test-common:${versions.kotlin}",
                            'annotations': "org.jetbrains.kotlin:kotlin-test-annotations-common:${versions.kotlin}",
                            'jdk'        : "org.jetbrains.kotlin:kotlin-test-junit:${versions.kotlin}"
                    ],
            ],
            rx                      : [
                    java   : "io.reactivex.rxjava2:rxjava:${versions.rxJava}",
                    binding: "com.jakewharton.rxbinding2:rxbinding-kotlin:${versions.rxBinding}",
            ],
            android                 : [
                    support: [
                            appcompat       : "com.android.support:appcompat-v7:${versions.supportLibrary}",
                            design          : "com.android.support:design:${versions.supportLibrary}",
                            constraintLayout: "com.android.support.constraint:constraint-layout:${versions.constraintLayout}"
                    ],
                    arch   : [
                            lifecycle: [
                                    extensions     : "android.arch.lifecycle:extensions:${versions.androidArch}",
                                    reactivestreams: "android.arch.lifecycle:reactivestreams:${versions.androidArch}"
                            ],
                            core     : [testing: "android.arch.core:core-testing:${versions.androidArch}"]
                    ]
            ],
            annotations             : 'org.jetbrains:annotations:16.0.1',

            junit                   : "junit:junit:${versions.junit}"
    ]

    repositories {
        google()
        jcenter()
        gradlePluginPortal()
    }

    dependencies {
        classpath deps.androidPlugin
        classpath 'com.github.ben-manes:gradle-versions-plugin:0.17.0'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:${versions.kotlin}"
        classpath 'com.moowork.gradle:gradle-node-plugin:1.2.0'
    }
}

subprojects {
    repositories {
        google()
        mavenCentral()
        jcenter()
    }

    tasks.withType(Test) {
        testLogging {
            events "failed"
            exceptionFormat "full"
            showExceptions true
            showStackTraces true
            showCauses true
        }
    }
}