apply plugin: 'com.github.ben-manes.versions'

buildscript {
    ext.versions = [
            'minSdk'          : 16,
            'compileSdk'      : 28,
            'targetSdk'       : 28,
            'kotlin'          : '1.3.31',
            'androidPlugin'   : '3.4.2',
            'supportLibrary'  : '28.0.0',
            'androidArch'     : '1.1.1',
            'rxJava'          : '2.2.2',
            'rxBinding'       : '3.0.0-SNAPSHOT',
            'junit'           : '4.12'
    ]


    ext.deps = [
            androidPlugin: "com.android.tools.build:gradle:${versions.androidPlugin}",
            kotlin       : [
                    stdlib: [
                            'common': "org.jetbrains.kotlin:kotlin-stdlib-common:${versions.kotlin}",
                            'jdk'   : "org.jetbrains.kotlin:kotlin-stdlib:${versions.kotlin}",
                            'js'    : "org.jetbrains.kotlin:kotlin-stdlib-js:${versions.kotlin}",
                    ],
                    test  : [
                            'common'     : "org.jetbrains.kotlin:kotlin-test-common:${versions.kotlin}",
                            'annotations': "org.jetbrains.kotlin:kotlin-test-annotations-common:${versions.kotlin}",
                            'jdk'        : "org.jetbrains.kotlin:kotlin-test-junit:${versions.kotlin}",
                            'js'         : "org.jetbrains.kotlin:kotlin-test-js:${versions.kotlin}",
                    ],
            ],
            rx           : [
                    java   : "io.reactivex.rxjava2:rxjava:${versions.rxJava}",
                    binding: "com.jakewharton.rxbinding3:rxbinding-kotlin:${versions.rxBinding}",
            ],
            android      : [
                    support: [
                            appcompat       : 'androidx.appcompat:appcompat:1.0.0',
                            design          : 'com.google.android.material:material:1.0.0',
                            constraintLayout: 'androidx.constraintlayout:constraintlayout:1.1.3'
                    ],
                    arch   : [
                            lifecycle: [
                                    livedata       : 'androidx.lifecycle:lifecycle-livedata:2.0.0',
                                    extensions     : 'androidx.lifecycle:lifecycle-extensions:2.0.0',
                                    reactivestreams: 'androidx.lifecycle:lifecycle-reactivestreams:2.0.0'
                            ],
                            core     : [testing: 'androidx.arch.core:core-testing:2.0.0']
                    ]
            ],
            annotations  : 'org.jetbrains:annotations:16.0.3',

            junit        : "junit:junit:${versions.junit}"
    ]

    repositories {
        google()
        jcenter()
        gradlePluginPortal()
        maven { url 'https://dl.bintray.com/kotlin/kotlin-eap' }
        maven { url "https://oss.sonatype.org/content/repositories/snapshots" }
    }

    dependencies {
        classpath deps.androidPlugin
        classpath 'com.github.ben-manes:gradle-versions-plugin:0.21.0'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:${versions.kotlin}"
        classpath "org.jetbrains.kotlin:kotlin-frontend-plugin:0.0.45"
    }
}

subprojects {
    repositories {
        google()
        mavenCentral()
        jcenter()
        maven { url "https://oss.sonatype.org/content/repositories/snapshots" }
        maven { url 'https://oss.sonatype.org/content/groups/public' }
    }

    tasks.withType(Test) {
        testLogging {
            events "passed", "skipped", "failed", "standard_error", "standard_out"
            exceptionFormat "full"
            showExceptions true
            showStackTraces true
            showCauses true
        }
    }
}
